{ ---------------------------------------------------------------------	}
{ функции для работы с FIFO буфером АЦП и ЦАП на плате L761,				}
{ 						на которой могут быть установлены ADSP-2184/2185/2186	}
{ ---------------------------------------------------------------------	}

{ *********************************************************************	}
{  Проверяем тип установленного DSP													}
{ *********************************************************************	}
CheckDspType:
{ прочитаем тип установленного на плате DSP										}
{ Для ADSP-2184 нужно чтобы DSP_Type=0x0,                        			}
{ для ADSP-2185 - DSP_Type=0x1, 		     	                     			}
{ а для ADSP-2186 - DSP_Type=0x2  		  		                    			}
	AR = DM(DSP_Type); AR = ABS AR; AY0 = 0x2;
	NONE = AR - AY0; 		{ Переменная DSP_Type должна быть: 0, 1 или 2	}
	IF GT AR = PASS 0;	{ иначе устанавливаем равной 0, т.е. ADSP-2184	}
	DM(DSP_Type) = AR;
{ установим нужный DMOVLAY и PMOVLAY												}
	AY0 = 0x1; NONE = AR - AY0; AR = 0x0;
	IF NE AR = PASS AY0;
	DMOVLAY = AR; PMOVLAY = AR;

	RTS;

{ !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!	}
{ *********************************************************************	}
{  Функция конфигурирования FIFO буфера АЦП 										}
{  устанавливаются значения адреса FIFO, его длина и место размещения  	}
{ *********************************************************************	}
CheckAdcFifoLimits:
	AR = DM(NewAdcFifoLength);	AR = ABS AR;
	AY0 = 0x200; NONE = AR - AY0;	{ минимально возможная длина FIFO АЦП	}
	IF LT AR = PASS AY0;
	DM(NewAdcFifoLength)=AR;

{ отконфигурируем FIFO АЦП согласно установленному DSP						}
	AR = DM(AdcFifoBaseAddressIndex);
	AR = ABS AR;
	IF EQ JUMP AdcFifoConfig0;
	AR = AR - 0x1;
	IF EQ JUMP AdcFifoConfig1;
	AR = AR - 0x1;
	IF EQ JUMP AdcFifoConfig2;

	RTS;

{ *********************************************************************	}
{  !!!!!  Только для ADSP-2185  !!!!!												}
{  ADC FIFO начинается с адреса DM 0x0 (0),										}
{  а его максимальная длина не более 0x3800 (14336=8192+4096+2048)		}
{ ********************************************************************* }
AdcFifoConfig0:
	AR = DM(DSP_Type); 			{ узнаем тип установленного на модуле DSP		}
	AR = ABS AR;AR = AR - 0x1;	{ если не ADSP-2185, то выходим					}
	IF NE RTS;

{ базовый адресс FIFO буфера АЦП для ADSP-2185										}
	AR = 0x0; DM(AdcFifoBaseAddress) = AR;
	AR = DM(NewAdcFifoLength); AR = ABS AR;
	AY0 = 0x3800;
	NONE = AR - AY0;
	IF GT AR = PASS AY0;
	DM(AdcFifoLength) = AR;

	RTS;

{ ********************************************************************* }
{  !!!!!  Не для ADSP-2184  !!!!!													}
{  ADC FIFO начинается с адреса DM 0x2000 (8192),								}
{  а его максимальная длина не более 0x1800 (4096+2048=6144)				}
{ ********************************************************************* }
AdcFifoConfig1:
	AR=DM(DSP_Type);				{ узнаем тип установленного на модуле DSP	}
	AR = PASS AR;					{ если ADSP-2184, то выходим					}
	IF EQ RTS;

	AR = 0x2000; DM(AdcFifoBaseAddress) = AR;
	AR = DM(NewAdcFifoLength); AR = ABS AR;
	AY0 = 0x1800;
	NONE = AR - AY0;
	IF GT AR = PASS AY0;
	DM(AdcFifoLength) = AR;

	RTS;

{ *********************************************************************	}
{  ADC FIFO начинается с адреса DM 0x2000 (8192) для ADSP-2184	и			}
{	 					с адреса DM 0x3000 (12288) для ADSP-2185 и ADSP-2186	}
{  а его максимальная длина не более 0x800 (2048)								}
{ ********************************************************************* }
AdcFifoConfig2:
	AR=DM(DSP_Type);			{ узнаем тип установленного на модуле DSP		}
	AR= PASS AR;
	AR=0x3000;              { если не ADSP-2184, то FIFO адрес с 0x3000	}
	IF EQ AR= PASS 0x2000;  { если ADSP-2184, то FIFO адрес с 0x2000		}

	DM(AdcFifoBaseAddress) = AR;
	AR = DM(NewAdcFifoLength); AR = ABS AR;
	AY0 = 0x800;
	NONE = AR - AY0;
	IF GT AR= PASS AY0;
	DM(AdcFifoLength) = AR;

	RTS;


{ !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!	}
{ ********************************************************************* }
{ Функция конфигурирования FIFO буфера ЦАП 										}
{ ********************************************************************* }
CheckDacFifoLimits:
	AR = DM(DSP_Type);			{ считаем тип установленного на модуле DSP}
	AR = ABS AR;
	IF EQ JUMP DacFifoConfig0; { установлен ADSP-2184							}
	AR = AR - 0x1;
	IF EQ JUMP DacFifoConfig1;	{ установлен ADSP-2185							}
	AR = AR - 0x1;
	IF EQ JUMP DacFifoConfig2; { установлен ADSP-2186							}

	RTS;

{ *********************************************************************	}
{  !!!!!  Для ADSP-2184  !!!!!														}
{  DAC FIFO начинается с адреса PM AR=0xC00 (3072),							}
{  а его максимальная длина не более AY0=0x400 (1024)							}
{ ********************************************************************* }
DacFifoConfig0:
	AR = 0xC00;               		{ базовый адрес FIFO ЦАП					}
	AY0 = 0x400; 						{ максимально возможная длина FIFO ЦАП	}

	JUMP SetDacFifoParam;

{ *********************************************************************	}
{  !!!!!  Для ADSP-2185  !!!!!														}
{  DAC FIFO начинается с адреса PM AR=0x3000 (12288),							}
{  а его максимальная длина не более AY0=0x1000 (4096)						}
{ ********************************************************************* }
DacFifoConfig1:
	AR = 0x3000;						{ базовый адрес FIFO ЦАП					}
	AY0 = 0x1000;						{ максимально возможная длина FIFO ЦАП	}

	JUMP SetDacFifoParam;

{ *********************************************************************	}
{  !!!!!  Для ADSP-2186  !!!!!														}
{  DAC FIFO начинается с адреса PM AR=0x1800 (6144),							}
{  а его максимальная длина не более AY0=0x800 (2048)							}
{ ********************************************************************* }
DacFifoConfig2:
	AR = 0x1800;              		{ базовый адрес FIFO ЦАП					}
	AY0 = 0x800;              		{ максимально возможная длина FIFO ЦАП	}

{ *********************************************************************	}
{ Установка параметров FIFO буфера ЦАП												}
{ *********************************************************************	}
SetDacFifoParam:
	DM(DacFifoBaseAddress) = AR;

	AR = DM(NewDacFifoLength);		{ требуемая длина FIFO ЦАП					}
	AR = ABS AR; NONE = AR - AY0;
	IF GT AR = PASS AY0;
	DM(DacFifoLength) = AR;			{ установленная длина FIFO ЦАП			}

	RTS;
